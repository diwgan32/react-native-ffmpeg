static String safePackageName(String prop) {
    prop.replace("-lts", "")
}

static String safePackageVersion(String prop, String version, String ltsVersion) {
    prop.contains("-lts") ? ltsVersion + ".LTS" : version
}

String safeExtGet(String prop, String fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion safeExtGet('reactNativeFFmpegPackage', 'https').contains("-lts") ? 16 : 24
        targetSdkVersion 29
        versionCode 50
        versionName "0.5.1"
        project.archivesBaseName = "react-native-ffmpeg"
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "$rootDir/node_modules/react-native/android" }
    mavenCentral()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    def work_version = "2.3.4"

    // (Java only)
    implementation "androidx.work:work-runtime:$work_version"
    implementation 'com.arthenica:mobile-ffmpeg-' + safePackageName(safeExtGet('reactNativeFFmpegPackage', 'https')) + ':' + safePackageVersion(safeExtGet('reactNativeFFmpegPackage', 'https'), '4.3.1', '4.3.1')
    implementation 'com.facebook.react:react-native:+'
}
